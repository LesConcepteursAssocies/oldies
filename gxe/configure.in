dnl ---------------------------------------------------------------------------
dnl Projet GXE
dnl
dnl philippe Guillaumaud <pguillaumaud@april.org>
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or
dnl (at your option) any later version.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA
dnl
dnl configure.in
dnl ---------------------------------------------------------------------------

dnl Version minimum requise de autoconf
AC_PREREQ(2.13)
dnl ---------------------------------------------------------------------------
dnl AC_INIT pointe sur un fichier qui doit toujours exister dans le projet.
dnl ---------------------------------------------------------------------------
AC_INIT(configure.in)
VERSION=`cat ${srcdir}/.version`

dnl ---------------------------------------------------------------------------
dnl définition de quelques constantes relatives au projet
dnl ---------------------------------------------------------------------------
GXE_MAJOR_VERSION=[`expr $VERSION : '\([0-9][0-9]*\)'`]
AC_SUBST(GXE_MAJOR_VERSION)
GXE_MINOR_VERSION=[`expr $VERSION : '[0-9][0-9]*\.\([0-9][0-9]*\)'`]
AC_SUBST(GXE_MINOR_VERSION)
GXE_MICRO_VERSION=[`expr $VERSION : '[0-9][0-9]*\.[0-9][0-9]*\.\([0-9][0-9]*\)'`]
AC_SUBST(GXE_MICRO_VERSION)

dnl ---------------------------------------------------------------------------
dnl Initialisation d'automake
dnl ---------------------------------------------------------------------------
AM_INIT_AUTOMAKE([gxe], $VERSION)

dnl ---------------------------------------------------------------------------
dnl détection du compilateur et autres outils nécessaires
dnl ---------------------------------------------------------------------------
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
dnl AC_PROG_CXX

dnl ---------------------------------------------------------------------------
dnl options de compilation par défaut
dnl ---------------------------------------------------------------------------
CFLAGS="-O2"
AC_SUBST(CFLAGS)

dnl ---------------------------------------------------------------------------
dnl warnings maximum (merci loic...)
dnl ---------------------------------------------------------------------------
AC_COMPILE_WARNINGS

dnl ---------------------------------------------------------------------------
dnl options de debug
dnl ---------------------------------------------------------------------------
AC_ARG_ENABLE(debug, 
[  --enable-debug=[no/minimum/yes] turn on debugging [default=no]],
  ,
  enable_debug=no)
if test "x$enable_debug" = "xyes"; then
  DEBUG_FLAGS="-g -DGXE_DEBUG"
else
  if test "x$enable_debug" = "xno"; then
    DEBUG_FLAGS=""
  else
    DEBUG_FLAGS=""
  fi
fi
AC_SUBST(DEBUG_FLAGS)

dnl ---------------------------------------------------------------------------
dnl détection de GLIB
dnl ---------------------------------------------------------------------------
AM_PATH_GLIB(1.2.6,,AC_MSG_ERROR(GLIB not installed, or glib-config not in path (version 1.2.6+ required!)))
AC_CHECK_PROG(GLIB_CONFIG, glib-config, glib-config)
if test "$GLIB_CONFIG" != ""
then
  GLIB_LIBS="`$GLIB_CONFIG --libs`"
  GLIB_CFLAGS="`$GLIB_CONFIG --cflags`"
else
  AC_MSG_ERROR(Can't find glib-config.)
fi
GLIB_LIBS="$GLIB_LIBS -lgmodule"
AC_SUBST(GLIB_LIBS)
AC_SUBST(GLIB_CFLAGS)

dnl ---------------------------------------------------------------------------
dnl Checks for dynamic linking
dnl ---------------------------------------------------------------------------
AC_CHECK_LIB(dl,dlopen)
if test "$ac_cv_lib_dl_dlopen" = "yes"; then
  AC_CHECK_FUNCS(dlopen)
else
  AC_CHECK_FUNCS(dlopen)
  if test "$ac_cv_func_dlopen" = "no"; then
    AC_MSG_ERROR(Can't find dlopen.)
  fi
fi
CFLAGS="$CFLAGS -rdynamic"
AC_SUBST(CFLAGS)

dnl ---------------------------------------------------------------------------
dnl Checks for pthread library
dnl ---------------------------------------------------------------------------
AC_CHECK_LIB(pthread,pthread_create)
if test "$ac_cv_lib_pthread_pthread_create" = "yes"; then
  AC_CHECK_FUNCS(pthread_create)
else
  AC_CHECK_FUNCS(pthread_create)
  if test "$ac_cv_func_pthread_create" = "no"; then
    AC_MSG_ERROR(Can't find pthread library.)
  fi
fi
GXE_THREAD_CFLAGS="-D_REENTRANT"
AC_SUBST(GXE_THREAD_CFLAGS)

dnl ---------------------------------------------------------------------------
dnl Locate the gnome-xml library
dnl ---------------------------------------------------------------------------
AC_CHECK_PROG(XML_CONFIG, xml-config, xml-config)
if test "$XML_CONFIG" != ""
then
  AC_MSG_CHECKING(for libxml >= 2.2.3)
  vers=`$XML_CONFIG --version | sed -e "s/libxml //" | awk 'BEGIN {FS=".";} { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
  if test "$vers" -ge 2002003; then
    AC_MSG_RESULT(found)
  else
    AC_MSG_ERROR(you need libxml >= 2.2.3 for this version of gxe)
  fi
  XML_LIBS="`$XML_CONFIG --libs`"
  XML_CFLAGS="`$XML_CONFIG --cflags`"
else
  AC_MSG_ERROR(Can't find gnome-xml library.)
fi
AC_SUBST(XML_LIBS)
AC_SUBST(XML_CFLAGS)

dnl ---------------------------------------------------------------------------
dnl Génération des Makefiles
dnl ---------------------------------------------------------------------------
AC_OUTPUT([
Makefile
src/Makefile
])
